if (rootProject.name == "kestra") {
    processResources {
        dependsOn(':ui:assembleFrontend')
    }
}

configurations {
    implementation.extendsFrom(micronaut)
}

dependencies {

    annotationProcessor "io.micronaut.openapi:micronaut-openapi"
    compileOnly "io.micronaut.openapi:micronaut-openapi-annotations"

    annotationProcessor project(':processor')
    implementation project(":core")

    implementation "io.micronaut:micronaut-management"
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut:micronaut-http-server-netty"
    implementation "io.micronaut.cache:micronaut-cache-core"
    implementation "io.micronaut.cache:micronaut-cache-caffeine"

    implementation("com.posthog.java:posthog:1.2.0")

    // ai
    implementation("dev.langchain4j:langchain4j")
    implementation("dev.langchain4j:langchain4j-google-ai-gemini")
    implementation("dev.langchain4j:langchain4j-http-client-jdk")
    implementation('org.bouncycastle:bcpkix-jdk18on')

    implementation("de.siegmar:fastcsv")

    // test
    testAnnotationProcessor project(':processor')
    testImplementation project(':core').sourceSets.test.output
    testImplementation project(':storage-local')
    testImplementation project(':worker')
    testImplementation "org.wiremock:wiremock-jetty12"
    testImplementation "org.awaitility:awaitility"
    testImplementation "io.opentelemetry:opentelemetry-sdk-testing"

    testImplementation project(':tests')
    testImplementation project(':jdbc')
    testImplementation project(':jdbc').sourceSets.test.output
    testImplementation project(':jdbc-h2')
    testImplementation("io.micronaut.sql:micronaut-jooq")
}

tasks.register('updateOpenapiVersion') {
    dependsOn processResources

    doLast {
        def openapiFilePath = "${buildDir}/classes/java/main/META-INF/swagger/kestra.yml"
        def openapiFile = file(openapiFilePath)

        if (openapiFile.exists()) {
            def originalText = openapiFile.text
            def updatedText = originalText.replace('version: 1.0.0', "version: ${project.version}")

            if (updatedText != originalText) {
                openapiFile.text = updatedText
                println "Successfully updated 'version' in $openapiFilePath to ${project.version}"
            } else {
                println "Warning: Did not find 'version: 1.0.0' to replace in $openapiFilePath. Check OpenAPI generator output."
            }
        } else {
            println "Error: OpenAPI file not found at $openapiFilePath. Skipping version update."
        }
    }


}

tasks.register('generateOpenapiSpec') {
    dependsOn ':webserver:compileJava'
    outputs.file("${project.getParent().projectDir}/openapi.yml")
    doLast {
        copyGeneratedOpenapiSpecToRoot()
    }
}

tasks.named('jar') {
    dependsOn updateOpenapiVersion
}

tasks.named('compileJava') {
    outputs.file("${project.getParent().projectDir}/openapi.yml")
    doLast {
        copyGeneratedOpenapiSpecToRoot()
    }
}

def copyGeneratedOpenapiSpecToRoot() {
    def sourceFile = file(project.layout.buildDirectory.file("classes/java/main/META-INF/swagger/kestra.yml"))
    def targetFile = file("${project.getParent().projectDir}/openapi.yml")

    if (!sourceFile.exists()) {
        throw new GradleException("OpenAPI file not found at ${sourceFile.absolutePath}")
    }

    copy {
        from sourceFile
        into project.getParent().projectDir
        rename { 'openapi.yml' }
    }

    println "Copied OpenAPI spec to ${targetFile}"
}
