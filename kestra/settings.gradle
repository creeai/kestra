plugins {
    id("com.gradle.develocity") version "4.3.2"
    id("com.gradle.common-custom-user-data-gradle-plugin") version "2.4.0"
}
def isCI = System.getenv('CI') != null
develocity {
    server = "https://develocity.kestra.io"
    buildScan {
        uploadInBackground = !isCI
        publishing.onlyIf {
            it.authenticated
        }
        buildScanPublished { scan ->
            if (isCI) {
                def payload = [
                    timestamp: new Date().toString(),
                    taskNames: gradle.startParameter.taskNames,
                    buildScanId: scan.buildScanId,
                    buildScanUri: scan.buildScanUri.toString()
                ]
                file("develocity-scan-output.ndjson") << groovy.json.JsonOutput.toJson(payload) + System.lineSeparator()
            }
        }
    }
}

rootProject.name = "kestra"


include 'platform'

include 'cli'
include 'core'
include 'tests'

include 'runner-memory'
include 'repository-memory'

include 'storage-local'

include 'jdbc'
include 'jdbc-h2'
include 'jdbc-mysql'
include 'jdbc-postgres'

include 'webserver'
include 'executor'
include 'scheduler'
include 'worker'

include 'ui'
include 'model'
include 'processor'
include 'script'

include 'jmh-benchmarks'
